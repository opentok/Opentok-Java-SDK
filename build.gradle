plugins {
    id 'java-library'
    id 'jacoco'
    id 'signing'
    id 'maven-publish'
    id "com.github.hierynomus.license" version "0.16.1"
}

group = 'com.tokbox'
archivesBaseName = 'opentok-server-sdk'
version = '4.7.0'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.github.tomakehurst:wiremock:1.58'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'commons-validator:commons-validator:1.7'
    implementation 'org.asynchttpclient:async-http-client:2.12.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.2.2'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'org.bitbucket.b_c:jose4j:0.7.11'
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}
javadoc {
    title = 'OpenTok Java SDK Reference'
    destinationDir = file('docs')
    exclude '**/util/**', '**/constants/**'
    options {
        locale 'en_US'
        setMemberLevel JavadocMemberLevel.PUBLIC
    }
}

jacoco {
    toolVersion = "0.8.8"
}
jacocoTestReport {
    reports {
        xml.getRequired().set(true)
        html.getRequired().set(true)
    }
}
check.dependsOn jacocoTestReport

license {
    header rootProject.file('codequality/HEADER')
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
}

artifacts {
    archives javadocJar, sourcesJar
}

ext.isReleaseVersion = hasProperty('isReleaseVersion') ? isReleaseVersion : false
ext.ossrhUsername = hasProperty('ossrhUsername') ? ossrhUsername : 'dummyuser'
ext.ossrhPassword = hasProperty('ossrhPassword') ? ossrhPassword : 'dummypass'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "OpenTok Java SDK"
                artifactId = 'opentok-server-sdk'
                packaging = 'jar'
                description = "The OpenTok Java SDK lets you generate sessions and tokens for OpenTok applications. This version of the SDK also includes support for working with OpenTok 2.0 archives."
                url = "https://github.com/opentok/opentok-java-sdk"
                licenses {
                    license {
                        name = 'MIT License'
                        url = "https://raw.github.com/opentok/opentok-java-sdk/main/LICENCE.txt"
                    }
                }
                developers {
                    developer {
                        id = "devrel"
                        name = "Vonage Devrel"
                        email = "devrel@vonage.com"
                    }
                    developer {
                        id = 'manasdpradhan'
                        name = 'Manas Pradhan'
                        email = 'manas@tokbox.com'
                        organization = 'Vonage'
                    }
                    developer {
                        id = 'aoberoi'
                        name = 'Ankur Oberoi'
                        email = 'aoberoi@gmail.com'
                        organization = 'Vonage'
                    }
                    developer {
                        id = 'slorello89'
                        name =  'Steve Lorello'
                        email = 'steve.lorello@vonage.com'
                        organization = 'Vonage'
                    }
                    developer {
                        id = 'SMadani'
                        name = 'Sina Madani'
                        email = 'sina.madani@vonage.com'
                        organization = 'Vonage'
                    }
                }
                scm {
                    connection = "scm:git@github.com/opentok/opentok-java-sdk"
                    developerConnection = "scm:git@github.com/opentok/opentok-java-sdk"
                    url = "http://github.com/opentok/opentok-java-sdk"
                }

                issueManagement {
                    system = "GitHub"
                    url = "https://http://github.com/opentok/opentok-java-sdk"
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            def snapshotsRepoUrl = uri("https://oss.sonatype.org/content/repositories/snapshots/")
            credentials.username(System.getenv("OSS_USERNAME"))
            credentials.password(System.getenv("OSS_PASSWORD"))
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    def signingKey = System.getenv("signingKey")
    def signingPassword = System.getenv("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
